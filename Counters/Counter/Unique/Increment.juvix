module Counter.Unique.Increment;

import Stdlib.Prelude open;
import Applib open;
import Counter.Unique.Resource open;
import Stdlib.Debug open;

increment
  {M : Type -> Type}
  {{Monad M}}
  {{Tx M}}
  (currentCounter : Resource)
  (logic : Logic)
  : M Transaction :=
  if
    | not (hasCounterKind currentCounter logic) :=
      failwith "The input resource has the wrong kind"
    | else :=
      do {
        nonce <- genRandomNonce;
        let
          newCounter :=
            mkCounter@{
              logic;
              nonce;
              uniqueLabel :=
                currentCounter |> Resource.label |> Label.toNat |> builtinAnomaDecode;
              count := getCount currentCounter + 1;
              ephemeral := false;
            };
        in
        mkTransactionNoPublicData@{
          consumed := [currentCounter];
          created := [newCounter];
        };
      };

txContext (currentRoot : CommitmentRoot) : TxContext :=
  TxContext.mk@{
    caller := Universal.identity;
    currentRoot;
  };

main
  (randSeed : Nat)
  (currentCounter : Resource)
  (logic : Logic)
  (latestRoot : CommitmentRoot)
  : TransactionRequest :=
  TransactionRequest.build
    randSeed
    (txContext latestRoot)
    (increment currentCounter logic);
