module HelloWorld;

import Stdlib.Prelude open;
import Applib open;

open Shielded;

--- The label for HelloWorld resources
label : Label := Label.mk (builtinAnomaEncode "HelloWorld");

mkHelloWorldConsumedResource
  (nonce : Nat)
  (message : String)
  {ephemeral : Bool := true}
  : ConsumedResource :=
  ConsumedResource.mk@{
    label;
    value := AnomaAtom.mk (builtinAnomaEncode message);
    quantity := 1;
    nonce := Nonce.fromNat nonce;
    ephemeral;
    randSeed := 0;
  };

mkHelloWorldCreatedResource
  (nonce : Nat)
  (message : String)
  {ephemeral : Bool := false}
  : CreatedResource :=
  CreatedResource.mk@{
    label;
    value := AnomaAtom.mk (builtinAnomaEncode message);
    quantity := 1;
    nonce := Nonce.fromNat nonce;
    ephemeral;
    randSeed := 0;
    -- TODO: What should we put here?
    nullifierKeyCommitment := NullifierKeyCommitment.mk (ByteArray.zero 1);
  };

--- Produces a ;Transaction; that creates a HelloWorld ;Resource;
--- @param nonce A number used to ensure ;Resource; uniqueness.
--- @param message The message to store in the ;Resource;
helloWorldTransaction
  (logic : ByteArray) (nonce : Nat) (message : String) : Transaction :=
  let
    complianceInputs :=
      ComplianceInputs.mk@{
        consumedResource := mkHelloWorldConsumedResource nonce message;
        createdResource := mkHelloWorldCreatedResource nonce message;
        -- TODO: I don't know what to use for the following fields. I'm just
        -- using the values from the Elixir codebase examples.
        inputNullifierKey :=
          NullifierKey.fromByteArray (ByteArray.mk [Byte.fromNat 1]);
        merklePath := MerklePath.mk (replicate 32 (0, false));
        rcv := 3;
        ephRoot := 0x7981c0051e6133fdab9d5f578d2d9c656b263ce761dff56861351cba08c8279;
      };
    witnessJson := Json.object [];
  in createFromComplianceInputs [complianceInputs] [logic] [witnessJson] [logic] [witnessJson];

--- The function that is run to produce a Transaction to send to Anoma.
main (logic : ByteArray) (message : String) : TransactionRequest :=
  TransactionRequest.fromTransaction (helloWorldTransaction logic 0 message);
